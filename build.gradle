buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

configurations {
    deployerJars
}

repositories {
    maven {
        name = "arisux"
        url = "http://arisux.ddns.net/maven/"
    }
}

apply plugin: 'forge'
apply plugin: 'curseforge'
apply plugin: 'maven'

version = "4.0.0.${System.env.BUILD_NUMBER}"
group= "org.avp"
archivesBaseName = "aliensvspredator"

tasks.withType(JavaCompile) {
	sourceCompatibility = "1.7"
	targetCompatibility = "1.7"
	options.encoding = 'UTF-8'
}

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    runDir = "eclipse"
    mappings = "stable_12"
}

println "Minecraft Forge " + minecraft.version + " with mappings " + minecraft.mappings

dependencies {
    deployerJars 'org.apache.maven.wagon:wagon-ftp:2.2'
    compile 'com.arisux:mdxlib:1.0.0.36:deobf'
}

jar {
	archiveName = "[${minecraft.getApiVersion()}][${project.version}] ${project.archivesBaseName}.jar"
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
	archiveName = "[${minecraft.getApiVersion()}][${project.version}] ${project.archivesBaseName}-sources.jar"
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
	archiveName = "[${minecraft.getApiVersion()}][${project.version}] ${project.archivesBaseName}-deobf.jar"
    classifier = 'deobf'
}

artifacts {
    archives srcJar, deobfJar
}

curse {
    displayName = "${project.archivesBaseName} ${project.version}"
    artifact = file("${project.buildDir}/libs/[${minecraft.getApiVersion()}][${project.version}] ${project.archivesBaseName}.jar")
    projectId = '221641'
    apiKey = project.curseForgeApiKey
    releaseType = 'beta'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

idea {
    module {
        inheritOutputDirs = true 
    }
}

task("publishToMaven", dependsOn:"build") {
    description = "Uploads the artifacts generated during the build process to the arisux maven repository"

    uploadArchives {
        repositories.mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: "ftp://arisux.ddns.net/maven/") {
                authentication(userName: project.mavenUsername, password: project.mavenPassword)
            }
        }
    }
}